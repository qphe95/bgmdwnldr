plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.bgmdwldr.vulkan"
    compileSdk 34
    ndkVersion "26.2.11394342"

    defaultConfig {
        applicationId "com.bgmdwldr.vulkan"
        minSdk 24
        targetSdk 34

        ndk {
            abiFilters "arm64-v8a"
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ["src/main/assets"]
        }
    }

    packaging {
        jniLibs.useLegacyPackaging = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
}

def shadersDir = file("src/main/shaders")
def assetsDir = file("src/main/assets")

def ndkPath = System.getenv("ANDROID_SDK_ROOT") ?: System.getenv("ANDROID_HOME") ?: "/home/qphe/android-sdk"
def glslcPath = "${ndkPath}/ndk/26.2.11394342/shader-tools/linux-x86_64/glslc"

tasks.register("compileShaders") {
    inputs.files fileTree(shadersDir)
    outputs.dir assetsDir

    doLast {
        if (!shadersDir.exists()) {
            return
        }
        assetsDir.mkdirs()
        exec {
            commandLine glslcPath, "${shadersDir}/triangle.vert", "-o", "${assetsDir}/triangle.vert.spv"
        }
        exec {
            commandLine glslcPath, "${shadersDir}/triangle.frag", "-o", "${assetsDir}/triangle.frag.spv"
        }
    }
}

tasks.named("preBuild").configure {
    dependsOn("compileShaders")
}
